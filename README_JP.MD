# はじめに

COM3D2向けの、独自スクリプト（マークダウンとKAGスクリプトを合わせたような記法）でメイドさんの動きを記述できるようにするMODです。マークダウンエディタで記述・閲覧してください。



これまで、メイドさんにちょっとした動作（モーション・ボイス・表情など）をさせたいときでも以下のような難しさがありました。

- MOD作成が必要で環境構築などの敷居が高く難しい
- MODを修正したらゲームの再起動が必要で繰り返し作業が大変



　そこで、独自スクリプトによるメイドさん動作の記述と、ゲーム中でスクリプトを再読み込みをできるようにしました。



# 利用上の注意

本MODはまだバグも多く、実行によりゲームがクラッシュすることもあります。本MODを使用するときはいつゲームがクラッシュしても大丈夫なように事前にセーブをしておくことを推奨します。

また、本MODを利用するに当たり、問題が発生しても作者は一切の責任を負いかねます。自己責任でご利用ください。



# 動作環境

事前に以下のMOD/dllを導入してください。

- しばりす２
- ExPluginBase（PluginExt.dll）v0.3
  - asm256氏ご作成  https://github.com/asm256/ExPluginBase/releases より入手



※COM3D2 GP-01 Ver.1.27.1 x64 で動作確認

# インストール方法

1. 上記の「しばりす２」を導入し、UnityInjectorが動作する環境にする
2. 上記の「PluginExt.dll」を「Sybaris\UnityInjector」フォルダにコピー
3. 「UnityInjector」フォルダを「Sybaris」フォルダにコピー
4. 「Config」フォルダを「Sybaris\UnityInjector」フォルダにコピー
5. ゲーム起動後、コンソールに「`Loaded Plugin: 'Scriplay.*.*.*'`」 と表示されることを確認



# 簡単な使い方～スクリプトを実行する

本MODはスタジオモードでの利用を想定しています。



サンプルスクリプトの実行方法

1. スタジオモード起動
2. メイドさんを２人配置
3. 画面右下の「UI OFF」を選択して、Scriplay以外のウィンドウを消す
4. スクリプト一覧から、実行したいスクリプトを選択する。



# ゲーム中の操作画面

## Config画面

スクリプトの作成に当たり、簡易的にボイスやモーションなどを確認できます。

### prop復元

propはメイドさんに服を着せたり、道具を持たせたりするときに使用する機能です。

propを元に戻して、メイドさんを読み込み直後の姿にします。

### ボイス再生

ボイスファイル名（「H0_08095」「H0_08095.ogg」など）を指定すると、対応するボイスを再生します。

再生開始位置（秒）　を指定すると、ボイスの再生開始位置を変更できます。

再生時間（秒）　を指定すると、ボイスの再生時間を変更できます。

直近３つまでの再生したボイスは、ボタンから再実行できます。

### モーション再生

モーション名（「 kaiwa_tati_taiki_f 」「 kaiwa_tati_taiki_f .anm」など）を指定すると、対応するモーションを再生します。

直近３つまでの再生したモーションは、ボタンから再実行できます。

### 表情再生

表情名（「にっこり」など）を指定すると、対応する表情を再生します。

直近３つまでの再生した表情は、ボタンから再実行できます。

### prop切替

propファイル名（「 handitemr_racket_i_ 」「 handitemr_mugcup_i_ .menu」など）を指定すると、対応するpropを装備します。

### スクリプト実行

1行程度の短いスクリプト（「@show text=テストコメント表示 wait=3s」「@motion name=kaiwa_tati_syazai_taiki_f 」など）を実行します。


### カタログ生成

「メイドさんを自動操作してスクリーンキャプチャ」を繰り返して、カタログとなるHTMLファイルを生成します。HTMLファイルは「`\Sybaris\UnityInjector\Config\Scriplay\capture`」フォルダに生成され、HTMLのリンク先画像は「`\Sybaris\UnityInjector\Config\Scriplay\capture\image`」フォルダに保存されます。画像はゲームの全画面のキャプチャになるため、そのままだと見づらいと思います。適宜、画像一括編集ソフトでメイドさんだけ切り抜くなど編集してください。

#### カタログ生成時の注意

- 正常にスクリーンキャプチャするために、カタログ生成中はゲーム画面をアクティブにしておいてください。
- 強制終了したいときは「終了」ボタンを押してください。
- 実行には時間がかかるため、寝る前の実行などがおすすめです。



#### 生成可能なカタログ

- 表情カタログ
- メイドさんのモーションカタログ
- ご主人様のモーションカタログ
- メイドさんのpropのモーションカタログ



## VRでのスクリプト実行

VRモードでは、操作用タブレットの下にスクリプト一覧が表示され、ここからスクリプトの実行が可能です。HTC-VIVEでのみ動作確認しています。（たぶん、他のVR環境でも動くはず）。



### 操作方法

コントローラをカメラモードにしてから、方向キーで以下のように操作します。

- ↑↓：　カーソル移動
- →：　確定
  - スクリプトの選択
  - 選択肢の選択
- ←長押し
  - スクリプト一覧の表示中：　スクリプトの再読込
  - スクリプトの実行中：　　　停止



# スクリプトの概要

スクリプトは実行スクリプトを記述するスクリプトファイル（`*.md`）と、スクリプトから参照できるリソースのテーブル（表情やボイスにカテゴリ名を付けてアクセスしやすくするもの）を記述するリソーステーブルファイル（`*.csv`）からを記述することで、スクリプトを作ります。



## スクリプトファイル（`*.md`）

以下のフォルダに配置されたマークダウンファイル（*.md）はゲーム中からスクリプトとして読み出し・実行できます。ゲーム実行中に再読み込み可能。ファイルはUTF-8エンコーディングとすること。

```
Sybaris\UnityInjector\Config\Scriplay\scripts
```



### ライブラリ（`lib_*.md`）

ファイル名が「`lib_*.md`」に該当するファイル（「lib_general.md」など）はライブラリとして扱われ、スクリプトの実行時に自動的に読み込まれます。多くのスクリプトで利用する共通のサブルーチンやグローバル変数のように扱いたい変数があるときはライブラリにしておくとよいです。



##  リソーステーブルファイル（`*.csv`）

以下のフォルダに配置された、特定の接頭辞がついたCSVファイル（*.csv）はカテゴリ定義ファイルとして扱われます。ゲーム実行中に再読み込み可能。ファイルはUTF-8エンコーディングとすること。

```
Sybaris\UnityInjector\Config\Scriplay\csv
```



下表の接頭辞がついたCSVファイルはすべて読み込まれ、カテゴリが定義されます。

|   接頭辞   |                  定義                  |    ファイル名の例     |
| :--------: | :------------------------------------: | :-------------------: |
| loopvoice_ | ループボイス（繰り返し発声するボイス） | loopvoice_default.csv |
| oncevoice_ | ワンスボイス（一度だけ発声するボイス） | oncevoice_default.csv |
|   face_    |                  表情                  |   face_default.csv    |



カテゴリにはスクリプトからアクセスできます。

例えば、ループボイスカテゴリは以下のように呼び出せます。

```
@talkRepeat maid=0 category=通常０
```





## スクリプト動作の概要

- 1行目から順番に実行
- 構文・コマンドとして解釈できなかった行は読み飛ばし
- １フレームに１コマンド実行される
  - マルチステートメントの場合は1行内に記述したコマンドすべてが実行される。
- エラーなどはコンソールに出力される



## コマンドの概要
- ほとんどのコマンドは`@（コマンド名） （パラメータ名）=（値）`の形式で記述される
- 各コマンドにはパラメータが指定可能
  - （パラメータ名）=（パラメータ値）　で記述
    - 「=」の間に半角スペースなど入れないように注意！
  - 半角スペースorタブ文字　で複数パラメータ記述可能
- コマンド名・パラメータ名ともに、すべて小文字に直してから解釈される
  - たとえば、`@talkrepeat`と`@talkRepeat`は同じコマンド
- パラメータの指定順序は何でもよい。入れ替えたりしても結果は同じ。
- 例：`@pos maid=0 x=0 y=0 z=0`





## 多くのコマンドに共通のパラメータについて

- 「maid」パラメータ
  - コマンドの操作対象のメイドさんを整数で指定。
    - （スタジオモードで）メイドさんを選択した順に「0, 1, 2, ...」と番号づけ
    - 例：`@face maid=0 name=きょとん`
  - 「指定なし」または「`all`と指定した」ときはすべてのメイドさんが操作対象になる
    - 例：全員がにっこりする
      - `@face name=にっこり`
      - `@face name=にっこり maid=all`
- 「man」パラメータ
  - コマンドの操作対象のご主人様を整数で指定。
  - 使い方は「maid」パラメータとほぼ同じ。
  - ただし、ご主人様は実行できないコマンドもあるので注意（表情再生、ボイス再生など）。
- 「name」「category」パラメータ
  - どちらかで動作内容を指定
    - 「name」ならリソース名を指定
      - （モーションファイル名、表情名　など）
    - 「category」ならカテゴリ名を指定
      - カテゴリは複数リソース名をまとめて１つのカテゴリとして定義したもの
      - カテゴリはCSVファイルで定義
- 「wait」パラメータ
  - コマンド実行時の待ち時間を小数で指定
    - 例：`@show text=一瞬だけ表示 wait=0.9s`
  - 単位はつけてもつけなくてもよい
    - いずれもOK：「wait=3sec」「wait=3s」「wait=3」
  - 時間経過したら（コマンド実行中でも）次の行が開始される
- 「fade」パラメータ
  - ゆっくり変化するときの移行時間（秒）を小数で指定
    - 例１：`@motion name=kaiwa_tati_tere_taiki_f fade=0.1s`
    - 例２：`@motion name=kaiwa_tati_tutorial_2_taiki_f fade=5s`
  - 単位はつけてもつけなくてもよい



## 構文関連

### ラベル

「#」から始まる行はラベルとして認識される。

「@goto」コマンドでのジャンプ先・「@call」コマンドの呼び出し先に指定できる。

> マークダウンエディタでスクリプトを閲覧するときは見出しとして表示され、エディタの「見出しマップ」や「アウトライン」機能で一覧できるので、スクリプトの処理単位ごとにラベルを付けておくとよい。



### コメント

「//」から始まる行はコメントとして認識され、実行されない。

> 解釈できない行との違いはコンソールに解釈できない旨をエラー表示されるか否か。



## 変数

`$（変数名）=（値）`で変数定義できる。値はすべて文字列として扱われる。
`${（変数名）}`で変数を呼び出すことができる。呼び出す変数の書かれた行が実行される前に、変数の部分が値で置き換えられてから、コマンドが評価されるようになっている。

例：
```
//変数定義
$motionName=kaiwa_tati_yo_taiki_f
//変数呼び出し
@motion name=${motionName}
```



## マルチステートメント

「;（セミコロン）」区切りで、1行に複数のコマンドを列挙することができる。「`@wait`」など一時停止系の操作はうまく動作しないことがあるので注意。

例：

```
@selection
- 喜ぶ exec=@motion name=kaiwa_tati_yorokobub_f_once_; @talk name=H0_01954
- たたく exec=@sound name=slap_high;                   @talk name=H1_02019

@wait 3s

```





## サブルーチン

`@call （ラベル名）`で、バッチファイルのようにラベル先をサブルーチンとして呼び出すことができる。指定のラベルへジャンプ後、`@exit`が実行されると呼び出し元へ処理が戻る。

サブルーチンとして呼び出すラベルは、最後に`@exit`で終わるように注意すること。



### 初期化サブルーチン

「`init_`」で始まるラベルは、初期化処理用のサブルーチンとして認識され、スクリプトを読込んだ後に自動的に実行される。



## スクリプトの起動順序

スクリプトの起動後は以下の順序で実行される。

1. ライブラリ（`lib_*.md`）の読み込み
2. 選択したスクリプト（`*.md`）の読み込み
3. すべてのラベルの行番号を記録
4. 初期化サブルーチン（`init_`で始まるラベル）の実行
5. 選択したスクリプトの1行目から処理開始



## Scriplay座標系

スクリプト実行のための座標系（Scriplay座標系）を定義して、そこからの相対値でメイドさんやカメラの位置を指定することができる。

![image-20191129214157993](readme.assets/image-20191129214157993.png)





# コマンドのリファレンス

コマンドの一覧と詳細な使用方法。

実際にスクリプトを作成するときは、同梱されているサンプルスクリプトからコピペした方が早いかも。







## 設定系コマンド

### @selection

選択肢ボタンを表示する。選択肢ボタンを選んだ場合の処理も記述できる。



記述形式

```
@selection （パラメータ）
- （選択肢に表示する文字） goto=（ジャンプ先）
- （選択肢に表示する文字） call=（呼び出すサブルーチン）
- （選択肢に表示する文字） exec=（実行する処理）

```





| パラメータ名 | 必須？ | 使い方                                                       |
| ------------ | ------ | ------------------------------------------------------------ |
| wait         | 任意   | 選択肢を表示したまま処理を一時停止する時間（秒）。選択肢が選ばれずに指定時間が過ぎると@selection以降の処理を再開する。<br />デフォルト：1年（つまり、選択肢ボタンを表示したままずっと一時停止。選択肢が選ばれるまで処理は一時停止） |
| keep         | 任意   | １：選択肢以降にスクリプトの処理を進める場合でも選択肢を選択可能なまま表示しておく。<br />０：選択肢以降にスクリプトの処理を進めた場合は選択肢を非表示<br />デフォルト：０ |
| mode         | 任意   | gotolist：実行中スクリプトのすべてのラベルに対してジャンプできる選択肢ボタンを末尾に追加する。スクリプトのデバッグ時に便利な機能。<br />none：（標準的な動作）<br />デフォルト：none |



例１）

```
@selection wait=3s
- 選択肢１ goto=ラベル１
- 選択肢２ exec=@show text=選択肢２が選ばれました。
@exit
# ラベル１
@show text=選択肢１が選ばれました。
@exit
```



### @show

テキストを表示する。テキスト表示中は処理は一時停止する。

| パラメータ名 | 必須？ | 使い方                                                       |
| ------------ | ------ | ------------------------------------------------------------ |
| text         | 必須   | 表示するテキストを指定                                       |
| wait         | 任意   | テキストを表示する時間（秒）<br />デフォルト：表示するテキストの長さに応じて自動的に決定。 |



例１：「こんにちは」と表示して、処理を一時停止する。しばらくしたら処理を再開して次の行が実行され、「さようなら」と表示される。

```
@show text=こんにちは
@show text=さようなら
```



例２：「こんにちは」と表示して、処理を5秒間、一時停止する。しばらくしたら処理を再開して次の行が実行され、「さようなら」と表示される。

```
@show text=こんにちは wait=5s
@show text=さようなら
```



### @origin

Scriplay座標系の原点の位置・向きを定義する。

| パラメータ名 | 必須？ | 使い方                                                       |
| ------------ | ------ | ------------------------------------------------------------ |
| maid         | 任意   | Scriplay座標系の原点を指定したメイドの位置・向きと同じになるように定義する。<br />デフォルト：指定なし |
| x            | 任意   | Scriplay座標系の原点のx座標をワールド座標系で指定。<br />デフォルト：Scriplay座標系原点の現在の値。 |
| y            | 任意   | Scriplay座標系の原点のy座標をワールド座標系で指定。<br />デフォルト：Scriplay座標系原点の現在の値。 |
| z            | 任意   | Scriplay座標系の原点のz座標をワールド座標系で指定。<br />デフォルト：Scriplay座標系原点の現在の値。 |
| rx           | 任意   | Scriplay座標系の向きをワールド座標系のx軸周りの向きで指定。<br />デフォルト：Scriplay座標系原点の現在の値。 |
| ry           | 任意   | Scriplay座標系の向きをワールド座標系のx軸周りの向きで指定。<br />デフォルト：Scriplay座標系原点の現在の値。 |
| rz           | 任意   | Scriplay座標系の向きをワールド座標系のx軸周りの向きで指定。<br />デフォルト：Scriplay座標系原点の現在の値。 |



例１：

Scriplay座標の原点をワールド座標系で指定

```
@origin x=1 y=1 z=1 rx=0 ry=0 rz=0      
```



例２：

Scriplay座標の原点をメイド位置０の位置・回転軸にセット

```
@origin maid=0            
```



### @goto

指定したラベルの行へジャンプして、そこから処理を再開する。



記述形式：

```
@goto （ラベル名）
```



例１：

```
@goto ラベル１
@show text=この行は実行されない

# ラベル１
@show text=この行は実行される。

```



### @call

指定したラベルをサブルーチンとして呼び出す。

（つまり、ラベルの行へジャンプして、そこから処理を再開し、`@exit`が実行されたら`@call`の実行された行まで戻ってくる。）



記述形式：

```
@call （ラベル名）
```



例１：

```
@call ラベル１
@show text=この行は２番目に実行される
@exit

# ラベル１
@show text=この行は１番目に実行される
@exit
```



### @exit

`@call`で呼び出されたサブルーチン内で実行すると、呼び出し元へ戻る。

そうでないときはスクリプトを終了する。



### @wait

指定時間（秒）だけ処理を一時停止する。単位は`sec`, `s`, （なし）のいずれでもよい。



記述形式：

```
@wait 3sec
```



### @soundrepeat

サウンドエフェクト系の音声をループ再生する。

| パラメータ名 | 必須？ | 使い方                                                       |
| ------------ | ------ | ------------------------------------------------------------ |
| name         | 必須   | 再生する音声ファイル名を指定<br />`stop`を指定した場合は音声停止。<br />また、下表の値を指定するとそれぞれ対応する音声を再生する。 |



| 設定値     | 再生する音声   |
| ---------- | -------------- |
| vibe_low   | 弱めのバイブ音 |
| vibe_high  | 強めのバイブ音 |
| kuchu_low  | 弱めのクチュ音 |
| kuchu_high | 強めのクチュ音 |
| slap_low   | 弱くたたく音   |
| slap_high  | 強くたたく音   |



例１：弱めのバイブ音を3秒再生してから音声を停止。
```
@soundrepeat name=vibe_low
@wait 3s
@soundrepeat name=stop
```



### @sound

サウンドエフェクト系の音声を１回だけ再生する。パラメータなどは`@soundrepeat`と同じ。



例１：弱くたたく音を再生

```
@sound name=slap_low

```



### @config





| パラメータ名 | 必須？ | 使い方                                                       |
| ------------ | ------ | ------------------------------------------------------------ |
| maid         | 任意   | 動作対象メイドをメイド番号で指定                             |
| man          | 任意   | 動作対象の男を番号で指定                                     |
| seikaku      | 任意   | 指定したメイドの性格を仮設定する。ボイスをカテゴリで指定した場合に、指定した性格に対応したボイスで再生されるようになる。スクリプト終了時にメイドの性格仮設定は解除される。<br />設定可能な値：Pride/Cool/Pure/Muku/Majime/Rindere<br />デフォルト：（指定なし） |
| visible      | 任意   | １：指定したメイドを表示する。<br />０：指定したメイドを非表示にする。<br />デフォルト：（指定なし） |
| restore      | 任意   | １：スクリプト終了時（コマンド終了時ではなく）に、すべてのメイドのPropをすべて初期状態に復元するよう予約する。<br />０：スクリプト終了時のPropの復元予約を解除する。<br />デフォルト：（指定なし） |



### @background

背景を変更する。



| パラメータ名 | 必須？ | 使い方                     |
| ------------ | ------ | -------------------------- |
| name         | 必須   | どの背景へ変更するかを指定 |

nameに指定可能な値：

| 値 | 表示される背景 |
| ------------ | ------ |
|Penthouse | ペントハウス|
|Shitsumu_ChairRot|執務室|
|Shitsumu_ChairRot_Night | 執務室（夜）|
|Salon | サロン|
|Syosai | 書斎|
|Syosai_Night | 書斎（夜）|
|DressRoom_NoMirror | ドレスルーム|
|MyBedRoom | 自室|
|MyBedRoom_Night | 自室（夜）|
|HoneymoonRoom | ハネムーンルーム（夜）|
|Bathroom | お風呂（夜）|
|PlayRoom | プレイルーム|
|PlayRoom2 | プレイルーム2|
|Pool | プール|
|SMRoom | SMルーム|
|SMRoom2 | 地下室|
|Salon_Garden | 中庭|
|LargeBathRoom | 大浴場|
|OiranRoom | 花魁部屋|
|Town | 街|
|Kitchen | キッチン|
|Kitchen_Night | キッチン（夜）|
|Salon_Entrance | エントランス|
|poledancestage | ポールダンス|
|Bar | バー（夜）|
|Toilet | トイレ|
|Soap | ソープ|
|MaidRoom | メイド部屋|



例１：背景をメイド部屋に変更

```
@background name=MaidRoom		メイド部屋
```



### @camera

カメラの観察対象を指定して、カメラの位置・向きを変更する。観察対象をメイドで指定した場合はメイドの正面、dパラメータで指定した距離へカメラが移動。観察対象を座標で指定した場合は、指定座標からdパラメータで指定した距離へカメラが移動する。

| パラメータ名 | 必須？ | 使い方                                                       |
| ------------ | ------ | ------------------------------------------------------------ |
| maid         | 任意   | 指定したメイドの前へカメラを移動<br />指定しない場合は、カメラの座標をパラメータで直接指示すること。<br />デフォルト：（指定なし） |
| x            | 任意   | カメラのX位置をScriplay座標系で指定（単位：メートル）。<br />デフォルト：現在の値 |
| y            | 任意   | カメラのy位置をScriplay座標系で指定（単位：メートル）。<br />デフォルト：現在の値 |
| z            | 任意   | カメラのz位置をScriplay座標系で指定（単位：メートル）。<br />デフォルト：現在の値 |
| rx           | 任意   | カメラのX軸周り向きをScriplay座標系で指定（単位：度）。<br />デフォルト：現在の値 |
| ry           | 任意   | カメラのy軸周り向きをScriplay座標系で指定（単位：度）。<br />デフォルト：現在の値 |
| rz           | 任意   | カメラのz軸周り向きをScriplay座標系で指定（単位：度）。<br />デフォルト：現在の値 |
| d            | 任意   | カメラと対象の距離を指定（単位：メートル）。<br />デフォルト：<br />　メイドを対象にカメラ移動する場合は1.5<br />　位置・向き指定でカメラ移動する場合は0 |


例１：1番目のメイドの正面、1.5m離れたところにカメラを移動する。

```
@camera maid=0
```





例２：Scriplay座標系で(0, 1.5, 0)の点に対して、正面から2ｍ離れたところにカメラを移動する。（例えば、身長1.5mのメイドがScriplay座標系で(x=0, z=0)のところにいた場合、そのメイドを正面から映すような位置へカメラが移動する。）

```
@camera x=0m y=1.5m z=0m ry=180deg rx=0deg d=2m
```



例３：Scriplay座標系で(1,1,1)の点に対して、30度見上げるように、0ｍ離れたところにカメラを移動する。（つまり、Scriplay座標系で(1,1,1)の点にへカメラを移動して、30度見上げる向きにする。）

```
@camera x=1m y=1m z=1m ry=0deg rx=-30deg
```



例４：Scriplay座標系で(1,1,1)の点に対して、30度見上げるように、１ｍ離れたところにカメラを移動する。

```
@camera x=1m y=1m z=1m ry=0deg rx=-30deg d=1m
```



### @fadein

画面を暗転する。

| パラメータ名 | 必須？ | 使い方                                                |
| ------------ | ------ | ----------------------------------------------------- |
| fade         | 任意   | 暗転にかかる時間（秒）を指定。<br />デフォルト：0.5秒 |



例１：画面を暗転する。

```
@fadein
```



### @fadeout

画面の暗転を解除して、ゲーム画面が見えるようにする。

パラメータは`@fadein`と同じ。



例１：画面の暗転を解除する。

```
@fadeout
```



### @info

スクリプトに関する情報を記述する。

| パラメータ名 | 必須？ | 使い方                                                       |
| ------------ | ------ | ------------------------------------------------------------ |
| info         | 任意   | スクリプトのバージョンを記述する。スクリプトバージョンの記述がない場合は、自動的に最新版のスクリプトと判別して実行する。<br />デフォルト：（指定なし） |



例１：スクリプトバージョン２であることをスクリプトファイルに示す。

```
@info version=2
```



### @bgm

BGMを変更する。

| パラメータ名 | 必須？ | 使い方                        |
| ------------ | ------ | ----------------------------- |
| name         | 必須   | 変更するBGMのファイル名を指定 |

例１：BGMを変更する。

```
@bgm name=bgm007
//「@bgm name=bgm007.ogg」でも同じ
```



### @require

スクリプトの実行に必要な要件を記述する。要件に満たない場合は、満たしていない要件を画面に大きく表示してスクリプトを強制終了する。

| パラメータ名 | 必須？ | 使い方                                                       |
| ------------ | ------ | ------------------------------------------------------------ |
| maidNum      | 任意   | スクリプトの実行に最低限必要なメイドの人数を指定<br />デフォルト：（指定なし） |
| manNum       | 任意   | スクリプトの実行に最低限必要な男の人数を指定<br />デフォルト：（指定なし） |



例１：メイドが２人以上配置されていなければスクリプトを終了する。

```
@require maidNum=2
```



## メイド操作系コマンド

### @pos

Scriplay座標系でメイド位置・向き（度）を指定。



| パラメータ名 | 必須？ | 使い方                                                       |
| ------------ | ------ | ------------------------------------------------------------ |
| maid         | 任意   | 動作対象メイドをメイド番号で指定                             |
| man          | 任意   | 動作対象の男を番号で指定                                     |
| x            | 任意   | メイドのx座標を座標系で指定（メートル単位）<br />デフォルト：現在の値。 |
| y            | 任意   | メイドのy座標を座標系で指定（メートル単位）。<br />デフォルト：現在の値。 |
| z            | 任意   | メイドのz座標を座標系で指定（メートル単位）。<br />デフォルト：現在の値。 |
| rx           | 任意   | メイドのx軸周り向きをScriplay座標系で指定（度単位、180で反転）。<br />デフォルト：現在の値。 |
| ry           | 任意   | メイドのy軸周り向きをScriplay座標系で指定（度単位、180で反転）。<br />デフォルト：現在の値。 |
| rz           | 任意   | メイドのz軸周り向きをScriplay座標系で指定（度単位、180で反転）。<br />デフォルト：現在の値。 |



例１：

１番目のメイドを、Scriplay座標系のx=1[m]の位置へ移動する。Scriplay座標系に対して90度右を向く。y,z座標はそのまま。

```
@pos maid=0 x=1 ry=90
```







### @face

指定したメイドの表情を変更する。



| パラメータ名 | 必須？             | 使い方                                                       |
| ------------ | ------------------ | ------------------------------------------------------------ |
| maid         | 任意               | 動作対象メイドをメイド番号で指定                             |
| name         | nameかcategory必須 | 再生する表情名を指定                                         |
| category     | 〃                 | 再生する表情カテゴリを指定                                   |
| fade         | 任意               | 表情の変更にかかる時間（秒）を指定。<br />デフォルト：１秒   |
| hoho         | 任意               | 赤面度を0-3の整数で指定　（指定なし）：現状維持              |
| namida       | 任意               | 涙の量を0-3の整数で指定　（指定なし）：現状維持              |
| yodare       | 任意               | 0：よだれなし　１：よだれあり　（指定なし）：現状維持        |
| eye          | 任意               | 指定値だけ両目を上に向ける。値は０～１の範囲で指定する。<br />デフォルト：（指定なし） |



例１：１番目のメイドに対して、3秒かけて、表情を「にっこり」に変更して、頬の赤みを3に、涙を3に、よだれをありにする。

```
@face maid=0 name=にっこり hoho=3 namida=3 yodare=1 fade=3sec
```



例２：１番目のメイドに対して、0秒かけて（つまり一瞬で）、表情をリソーステーブルファイルで指定した「通常０」カテゴリのどれかに指定して、頬の赤みを0に、涙を0に、よだれをなしにする。

```
@face maid=0 category=通常０ hoho=0 namida=0 yodare=0 fade=0sec
```



例３：2番目のメイドに対して、目を少し上に向けさせる。

```
@face maid=1 eye=0.5
```



### @motion

指定したメイドのモーションを変更する。

モーション名に`_once_`が含まれる場合は、対応する`_taiki_`モーションを探してきて、onceモーション再生後に自動的にtaikiモーションを再生する。

例：`@motion name=kaiwa_tati_yorokobub_f_once_ `



| パラメータ名 | 必須？ | 使い方                                                       |
| ------------ | ------ | ------------------------------------------------------------ |
| maid         | 任意   | 動作対象メイドをメイド番号で指定                             |
| man          | 任意   | 動作対象の男を番号で指定                                     |
| name         | 必須   | 再生するモーション名を指定                                   |
| finish       | 任意   | １：モーションの再生が終わるまでスクリプトを一時停止         |
| speed        | 任意   | モーションの再生速度を小数で指定<br />デフォルト：１         |
| afterSpeed   | 任意   | onceモーション再生後のtaikiモーションの再生速度を小数で指定<br />デフォルト：１ |
| fade         | 任意   | モーションの移行時間（秒）<br />デフォルト：0.8±20% sec      |
| similar      | 任意   | １：5秒ごとに、自動的に類似モーションへ変更                  |
| similarSec   | 任意   | 何秒ごとに、自動的に類似モーションへ変更するかを指定         |

例１

メイド０でdildo_onani_2a01_fを４倍速再生し、１秒ごとにdildo_onani_2で始まるモーション（dildo_onani_2b02_fなど）を４倍速再生する。

```
@motion maid=0 name=dildo_onani_2a01_f similarSec=1sec speed=4
```



例２：

dildo_onani_1b01_fを再生し始めてから、2秒かけて徐々に元のモーションから移行する。モーションが１周再生終わるまで処理は一時停止。モーション再生が１周したら、テキストを表示。

```
@motion maid=0 name=dildo_onani_1b01_f similarSec=1sec fade=2sec finish=1
@show text=モーション再生終わり
```



例３

kaiwa_tati_yorokobub_f_once_を再生し、
終了したら自動的にkaiwa_tati_yorokobub_taiki_fを2倍速で再生

```
@motion name=kaiwa_tati_yorokobub_f_once_ afterSpeed=2 fade=0.1sec
```





### @talk

指定したメイドで、ボイスを一度だけ再生する。以下、一度だけ再生するボイスをワンスボイスと呼ぶ。



| パラメータ名 | 必須？             | 使い方                                                       |
| ------------ | ------------------ | ------------------------------------------------------------ |
| maid         | 任意               | 動作対象メイドをメイド番号で指定                             |
| name         | nameかcategory必須 | 再生するワンスボイス名を指定                                 |
| category     | 〃                 | 再生するワンスボイスカテゴリを指定                           |
| finish       | 任意               | １：ワンスボイスの再生が終わるまでスクリプトを一時停止       |
| start        | 任意               | 音声ファイルの再生開始位置（秒）<br />デフォルト：0          |
| interval     | 任意               | 音声ファイルの再生時間（秒）。0なら最後まで再生。<br />デフォルト：0 |
| fadein       | 任意               | 再生開始してから、徐々に音量を大きくしていき音量が最大になるまでの時間（秒）。<br />デフォルト：0 |



例１：指定した音声ファイルの0.5秒の位置から再生開始して、最後まで音声を再生する。話しはじめは0.2秒かけてだんだん音声が大きくなる。音声再生が終わるまで処理は一時停止する。

```
//ぇ、面白いです！　ハマると時間を忘れて熱中しちゃいますよ。
@talk maid=0 name=H1_05828 start=0.5s finish=1 fade=0.2s
@show text=再生が終わったらこの文字が表示される。
```



例２：リソーステーブルファイル（`oncevoice_*.csv`というファイル）の「絶頂２」カテゴリからランダムに1つを選んで再生する。

```
@talk maid=0 category=絶頂２  

```



### @talkRepeat

指定したメイドで、ボイスをループ再生する。以下、ループ再生するボイスをループボイスと呼ぶ。

パラメータなどは`@talk`と同じ。



例１：例２：リソーステーブルファイル（`loopvoice_*.csv`というファイル）の「絶頂後」カテゴリからランダムに1つを選んで再生する。

```
@talkRepeat maid=0 category=絶頂後

```



### @shapekey

指定したメイドのシェイプキーのアニメーションを設定する。シェイプキー対応ボディでのみコマンドが動作する。

シェイプキー対応ボディでない場合や指定したシェイプキーが存在しない場合は、何も実行しない。

| パラメータ名 | 必須？                              | 使い方                                                       |
| ------------ | ----------------------------------- | ------------------------------------------------------------ |
| maid         | 任意                                | 動作対象メイドをメイド番号で指定                             |
| name         | 必須                                | 操作対象のシェイプキー名を指定。                             |
| mode         | 任意                                | 動作モードを指定。<br />デフォルト：fade<br /><br />- `fade` : fadeパラメータで指定した時間かけて、シェイプキー値をvalパラメータで指定した値へ変化させる。<br />-  `sin` : シェイプキー値をmaxからminの間で往復変化させる。変化速度は、時間経過に対してSin波状に変化する。periodパラメータで指定した時間（秒）が変化周期になる。<br />  - ![「sin 波形」の画像検索結果](readme.assets/Wed, 04 Dec 2019 213105.jpeg)<br /><br />- `triangle` : シェイプキー値をmaxからminの間で往復変化させる。変化速度は、時間経過に対して三角波状に変化する。periodパラメータで指定した時間（秒）が変化周期になる。<br />  - ![関連画像](readme.assets/trianglewave-1575462738614.png)<br />-  `keiren` : 痙攣しているように見えるようにシェイプキー値を変化させる。具体的にはシェイプキー値をmaxからminの幅の10分の１倍で高速に変化させ、１度だけmaxからminの幅と同じだけ変化する。これを１セットとして、おおむねperiodパラメータで指定した周期で繰り返す。周期はランダムに変動する。 <br /> |
| val          | fadeモードの場合必須                | fadeモードの場合、シェイプキー値を変化させる目標値を指定する。<br />それ以外の動作モードの場合、無視される。 |
| min          | sin,triangle,keirenモードの場合必須 | sin,triangle,keirenモードの場合、シェイプキー値を変化させる範囲の下限値を指定する。<br />それ以外の動作モードの場合、無視される。 |
| max          | sin,triangle,keirenモードの場合必須 | sin,triangle,keirenモードの場合、シェイプキー値を変化させる範囲の上限値を指定する。<br />それ以外の動作モードの場合、無視される。 |
| fade         | 任意                                | fadeモードの場合、何秒かけてシェイプキー値を目標値まで変化させるか指定する。<br />それ以外の動作モードの場合、無視される。<br />デフォルト：0sec |
| period       | sin,triangle,keirenモードの場合必須 | sin,triangle,keirenモードの場合、シェイプキー値を変化させる周期（秒）を指定する。<br />それ以外の動作モードの場合、無視される。 |
| finish       | 任意                                | sin,triangle,keirenモードの場合、何秒間シェイプキーの変化動作をするか指定する。<br />それ以外の動作モードの場合、無視される。<br />デフォルト：60sec |



例１：１番目のメイドの「boko」シェイプキー（お腹のふくらみ）を、０秒かけて、シェイプキー値を0に変化させる。（シェイプキーの設定をリセットしたいときとかに使用）

```
@shapekey maid=0 name=boko val=0
```



例２：１番目のメイドの「boko」シェイプキー（お腹のふくらみ）を、0.2秒かけて、シェイプキー値を1に変化させる。（連続的に形が変わっていくときに使用）

```
@shapekey maid=0 name=boko mode=fade val=1 fade=0.2
```



例３：１番目のメイドの「orgasm」シェイプキーを、シェイプキー値0から0.2の幅で、0.05秒周期でSin波状に変化させ、５秒経過したら停止する。

```
@shapekey maid=0 name=orgasm mode=sin min=0 max=0.2 period=0.05s finish=5s
```



例４：１番目のメイドの「orgasm」シェイプキーで痙攣動作させる。
```
@shapekey name=orgasm mode=keiren min=0 max=0.9 period=1s
```



### @setProp

指定したメイドにprop（服、アクセサリ、道具、髪などオブジェクト全般）を装着する。



| パラメータ名 | 必須？ | 使い方                                                       |
| ------------ | ------ | ------------------------------------------------------------ |
| maid         | 任意   | 動作対象メイドをメイド番号で指定                             |
| name         | 必須   | 装着するpropのファイル名                                     |
| part         | 任意   | 装着する部位を明示的に指定。基本的には指定する必要なし。<br /><br />指定しない場合は、propのファイル名から装着部位を自動判断するので、うまくpropが装備できないときだけ使用するパラメータ。<br />指定可能な値：<br />MuneL, MuneS, MuneTare, RegFat, ArmL, Hara, RegMeet, KubiScl, UdeScl, EyeScl, EyeSclX, EyeSclY, EyePosX, EyePosY, EyeClose, EyeBallPosX, EyeBallPosY, EyeBallSclX, EyeBallSclY, EarNone, EarElf, EarRot, EarScl, NosePos, NoseScl, FaceShape, FaceShapeSlim, MayuShapeIn, MayuShapeOut, MayuX, MayuY, MayuRot, HeadX, HeadY, DouPer, sintyou, koshi, kata, west, MuneUpDown, MuneYori, MuneYawaraka, body, moza, head, hairf, hairr, hairt, hairs, hairaho, haircolor, skin, acctatoo, accnail, underhair, hokuro, mayu, lip, eye, eye_hi, eye_hi_r, chikubi, chikubicolor, eyewhite, nose, facegloss, wear, skirt, mizugi, bra, panz, stkg, shoes, headset, glove, acchead, accha, acchana, acckamisub, acckami, accmimi, accnip, acckubi, acckubiwa, accheso, accude, accashi, accsenaka, accshippo, accanl, accvag, megane, accxxx, handitem, acchat, onepiece, set_maidwear, set_mywear, set_underwear, set_body, folder_eye, folder_mayu, folder_underhair, folder_skin, folder_eyewhite, kousoku_upper, kousoku_lower, seieki_naka, seieki_hara, seieki_face, seieki_mune, seieki_hip, seieki_ude, seieki_ashi,<br /><br />デフォルト：（指定なし） |



例１：１番目のメイドにdress217_glove_i_（手袋）を装着する。
```
@setProp maid=0 name=dress217_glove_i_
//@setProp maid=0 name=dress217_glove_i_ part=glove　　でも同じ。
```



例２：すべてのメイドに「_i_mywear017」（バニー服一式）を装着する。

```
@setprop name=_i_mywear017
```





### @delProp

指定したメイドからprop（服、アクセサリ、道具、髪などオブジェクト全般）を除去する。

パラメータは`@setProp`と同じ。



例１：１番目のメイドからdress217_glove_i_（手袋）を除去する。

```
@delProp maid=0 name=dress217_glove_i_
//@delProp maid=0 name=dress217_glove_i_ part=glove　　でも同じ。
```



### @setSlot

prop（服、アクセサリ、道具、髪などオブジェクト全般）を装着する。対象位置にpropが指定されていて、すでに除去済みの場合のみ、正常にコマンド動作する。



| パラメータ名 | 必須？ | 使い方                                                       |
| ------------ | ------ | ------------------------------------------------------------ |
| maid         | 任意   | 動作対象メイドをメイド番号で指定                             |
| name         | 必須   | 装着するスロット名を指定。<br />指定可能な値：<br />　複合系<br />all：「wear, mizugi, onepiece, bra, skirt, panz, glove, accUde, stkg, shoes, accKubi, accKubiwa」を同時に変更<br />overwear　：　「wear,  onepiece,  skirt,   shoes, accKubi, accKubiwa」を同時に変更<br />exceptacc　：　「wear, mizugi, onepiece, bra, skirt, panz,  stkg」を同時に変更<br /><br />　特定の部位のみ<br />body, head, eye, hairF, hairR, hairS, hairT, wear, skirt, onepiece, mizugi, panz, bra, stkg, shoes, headset, glove, accHead, hairAho, accHana, accHa, accKami_1_, accMiMiR, accKamiSubR, accNipR, HandItemR, accKubi, accKubiwa, accHeso, accUde, accAshi, accSenaka, accShippo, accAnl, accVag, kubiwa, megane, accXXX, chinko, chikubi, accHat, kousoku_upper, kousoku_lower, seieki_naka, seieki_hara, seieki_face, seieki_mune, seieki_hip, seieki_ude, seieki_ashi, accNipL, accMiMiL, accKamiSubL, accKami_2_, accKami_3_, HandItemL, underhair, moza, |




例１：１番目のメイドにwearを装着する。

```
@setSlot maid=0 name=wear
```





### @delSlot

prop（服、アクセサリ、道具、髪などオブジェクト全般）を除去する。対象位置にpropが指定されていて、すでに装着済みの場合のみ、正常にコマンド動作する。

パラメータは`@setSlot`と同じ。



例１：１番目のメイドからwearを除去する。

```
@delSlot maid=0 name=wear
```

例２：すべてのメイドの、すべての服・アクセサリを除去する。

```
@delSlot name=all
```



### @setParticle

指定したメイドのparticle（体から放出するようなオブジェクト）をONにする。

| パラメータ名 | 必須？ | 使い方                                                       |
| ------------ | ------ | ------------------------------------------------------------ |
| maid         | 任意   | 動作対象メイドをメイド番号で指定                             |
| name         | 必須   | 再生するparticleを指定。<br />可能な値：<br />- toiki1　：　吐息１<br />- toiki2　：　吐息２<br />- aieki1　：　愛液１<br />- aieki2　：　愛液２<br />- aieki3　：　愛液３<br />- nyo　：　尿<br />- sio　：　潮<br /> |



例１：１番目のメイドの吐息particleをONにする。

```
@setParticle maid=0 name=toiki1

```



### @delParticle

指定したメイドのparticle（体から放出するようなオブジェクト）をOFFにする。

パラメータは`@setParticle`と同じ。ただし、「name」パラメータにallを指定した場合はすべてのparticleをOFFにする。



例１：１番目のメイドの吐息particleをOFFにする。

```
@delParticle maid=0 name=toiki1

```

例２：すべてのメイドの、すべてのparticleをOFFにする。

```
@delParticle name=all

```





# CSVファイルの管理のコツ

CSVファイルはエクセルで、1シートを１CSVファイルに対応させて一括管理するとよいです。  エクセルの入力補助機能を活かして楽に作業できます。



エクセルからCSVへの出力手順

1. （マクロ使用のための設定、初回のみ実施）Visual Basic Editor のメニューから［ツール］→［参照設定］を選び，［参照可能なライブラリファイル］の中から "Microsoft ActiveX Data Objects x.x Library" にチェックを入れる
2. ↓のマクロを使用して各シートをCSVファイルとして出力
3. 出力したCSVファイルを「Sybaris\UnityInjector\Config\Scriplay\csv」フォルダに配置する



エクセルはデフォルトでShift-JISエンコーディングでファイル出力しますが、UnityはUTF-8しか読み込めないのでUTF-8で出力しています。



```vb
Sub writeCSV_UTF8_noBOM_CRLF()
'
'エクセルファイルと同じフォルダに全シートをBOMなしUTF-8のCSVとして保存
'

Dim ws As Worksheet, savePath As String

' ブックが保存済みでない場合は保存
If ActiveWorkbook.Saved = False Then
    If MsgBox("ブックがまだ保存されていません。保存しますか？", vbYesNo) = vbNo Then
        Exit Sub
    Else
        ActiveWorkbook.Save
    End If
End If

'全シート保存
Application.DisplayAlerts = False
Application.ScreenUpdating = False
For Each ws In ActiveWorkbook.Worksheets
    ws.Activate
    savePath = ActiveWorkbook.Path & "\" & ws.Name & ".csv"
    'ADODB.Streamオブジェクトを生成
    Dim adoSt As Object
    Set adoSt = CreateObject("ADODB.Stream")

    Dim strLine As String
    Dim i As Long, j As Long
    i = 1
    With adoSt
        .Charset = "UTF-8"
        .LineSeparator = adLF
        .Open

        Do While ws.Cells(i, 1).Text <> ""
            strLine = ""
            j = 1
            Do While True
                If ws.Cells(i, j + 1).Text = "" Then Exit Do
                strLine = strLine & ws.Cells(i, j).Text & ","
                j = j + 1
            Loop
            strLine = strLine & ws.Cells(i, j).Text & vbCrLf  ' 改行文字：\r\n
            .WriteText strLine
            i = i + 1
        Loop

        .Position = 0 'ストリームの位置を0にする
        .Type = adTypeBinary 'データの種類をバイナリデータに変更
        .Position = 3 'ストリームの位置を3にする

        Dim byteData() As Byte '一時格納用
        byteData = .Read 'ストリームの内容を一時格納用変数に保存
        .Close '一旦ストリームを閉じる（リセット）

        .Open 'ストリームを開く
        .Write byteData 'ストリームに一時格納したデータを流し込む
        .SaveToFile savePath, adSaveCreateOverWrite  'savePath:フルパス　adSaveCreateOverWrite:指定したファイルが既に存在する場合、上書き
        .Close
    End With

Next

End Sub

```





# 本MODを改変する場合について

本MODの改変・再配布は自由ですが、既存MODとの競合をさけるため、以下の点を守るようお願いいたします。

- MOD名称の変更

- 名前空間の変更

  - ソースコードのnamespaceを別の名前にする

    - ↓の「Scriplay」を別の名前にすればよい

    ```c#
    namespace COM3D2.Scriplay.Plugin
    ```

- 別のウィンドウIDを使用すること

  - 各ウィンドウIDを別の数字（10くらい上げた数字）に変更

  - 例（「21」の箇所）

    ```c#
    GUI.Window(21, node_main, WindowCallback_mainUI, cfg.PluginName + " Main UI", guiTitleStyle);
    ```

# スクリプト置き場
一応、スクリプト置き場を作成しました。ご自由にご利用ください。  

https://ux.getuploader.com/Scriplay_Scripts/



# 更新履歴

## Scriplay_0.2.0_COM3D2GP-01Ver.1.27.1x64.zip

| スクリプトバージョン | MODバージョン | 本体バージョン               |
| -------------------- | ------------- | ---------------------------- |
| 2                    | 0.2.0         | COM3D2GP-01Ver.1.27.1x64.zip |

- 2019年12月29日
- バージョニングルールをセマンティックバージョニングに変更。
- スクリプト動作に破壊的変更あり。
- VR、シェイプキー操作、prop操作、particle操作など機能の大幅追加。



## COM3D2.Scriplay.Plugin.Ver1.240.1.zip

| スクリプトバージョン | MODバージョン | 本体バージョン           |
| -------------------- | ------------- | ------------------------ |
| 1                    | 0.2.0         | COM3D2 Ver.1.24.1x64.zip |

- 2019年2月8日
- 初版



# 謝辞

本MODは公開されている様々なMODを参考に作成させていただきました。

素晴らしいプログラムを公開してくださった先達に感謝いたします。



# MOD使用上の注意

- MODはKISSサポート対象外です。
- MODを利用するに当たり、問題が発生してもKISSは一切の責任を負いかねます。
- 「カスタムオーダーメイド3D2」を購入されている方のみが利用できます。
- 「カスタムオーダーメイド3D2」上で表示する目的以外の利用は禁止します。
- 「カスタムメイド3D2」上では利用しないで下さい。
- これらの事項は http://kisskiss.tv/kiss/diary.php?no=558 を優先します。



# ライセンス

本MODはパブリックライセンス（WTFPL）が適用されています。

KISS規約の範囲内でご利用ください。
